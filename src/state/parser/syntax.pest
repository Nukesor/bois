whitespace = _{ " " | "\t" | NEWLINE }

// Comment syntax for various file formats.
// Can be prefixed with any amount of spaces.
comment = _{ whitespace* ~ ("//" | "#"  | "--" | "%" | "/*" | "*" | "**" | "*/" | "" )}

// The bois config is a block that's in between these start/end
config_delimiter = _{ comment ~ whitespace* ~ "bois_config" }

config_end_condition = _{ config_delimiter ~ ANY* }

// Allow multiple lines of input.
// Each line is only allowed to consist of a comment prefix followed by characters
// that aren't newlines.
line_behind_comment = @{ (!(NEWLINE) ~ ANY)* }
config_line = _{ comment ~ line_behind_comment }
// Stop the config content as soon as we hit the end block
config_content = {
    ( !config_end_condition ~ config_line)*
}

// The rest of the file
file_content = {
    ANY*
}

// Expect a commented block, that's sandwhiched between the prefix and suffix.
// It can be anywhere in the config file.
full_config = {
    SOI ~ config_delimiter ~ config_content ~ config_delimiter ~ file_content ~ EOI
}
